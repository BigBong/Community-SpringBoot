apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

configurations {
    mybatisGenerator
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile("org.springframework.boot:spring-boot-starter-jetty")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    testCompile("org.springframework.boot:spring-boot-starter-test")

    mybatisGenerator libraries.mybatis_generator
    mybatisGenerator libraries.mysql
    mybatisGenerator libraries.mybatis_mapper
}

def getProperties(fileName) {
    def properties = new Properties()
    file("src/main/resources/" + fileName).withInputStream { inputStream ->
        properties.load(inputStream)
    }
    properties;
}

task mybatisGenerate << {
    def mysqlProperties = getProperties("db-mysql.properties")
    def mybatisProperties = getProperties("mybatis.properties")

    def destDir = new File("src", 'main')

    ant.properties['targetProject'] = destDir.path
    ant.properties['driverClass'] = mysqlProperties.getProperty("jdbc.driverClassName")
    ant.properties['connectionURL'] = mysqlProperties.getProperty("jdbc.url")
    ant.properties['userId'] = mysqlProperties.getProperty("jdbc.user")
    ant.properties['password'] = mysqlProperties.getProperty("jdbc.pass")
    ant.properties['src_main_java'] = sourceSets.main.java.srcDirs[0].path
    ant.properties['src_main_resources'] = sourceSets.main.resources.srcDirs[0].path
    ant.properties['modelPackage'] = mybatisProperties.getProperty("modelPackage")
    ant.properties['mapperPackage'] = mybatisProperties.getProperty("mapperPackage")
    ant.properties['sqlMapperPackage'] = mybatisProperties.getProperty("sqlMapperPackage")

    ant.taskdef(
            name: 'mbgenerator',
            classname: 'org.mybatis.generator.ant.GeneratorAntTask',
            classpath: configurations.mybatisGenerator.asPath
    )
    ant.mbgenerator(overwrite: true,
            configfile: 'src/main/resources/mybatis/generatorConfig.xml', verbose: true) {
        propertyset {
            propertyref(name: 'targetProject')
            propertyref(name: 'userId')
            propertyref(name: 'driverClass')
            propertyref(name: 'connectionURL')
            propertyref(name: 'password')
            propertyref(name: 'src_main_java')
            propertyref(name: 'src_main_resources')
            propertyref(name: 'modelPackage')
            propertyref(name: 'mapperPackage')
            propertyref(name: 'sqlMapperPackage')
        }
    }
}